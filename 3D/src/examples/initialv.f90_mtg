! Initial condition for the velocity.
!
! This file contains the expression used for the initial
! velocity in spectral space. You can use temporary real arrays
! R1-R3 of size (1:nx,1:ny,ksta:kend) and temporary complex
! arrays C1-C8 of size (nz,ny,ista:iend) to do intermediate
! computations. The variable u0 should control the global
! amplitude of the velocity , and variables vparam0-9 can be
! used to control the amplitudes of individual terms. At the
! end, the three components of the velocity in spectral
! space should be stored in the arrays vx, vy, and vz.

! Superposition of Taylor-Green vortices
!     kdn : minimum wave number
!     kup : maximum wave number
!     u0  : velocity magnitude

      IF ( abs(Lx-Ly).gt.tiny ) THEN
        IF (myrank.eq.0) &
           PRINT *,'TG initial conditions require at least Lx=Ly'
        STOP
      ENDIF

!$omp parallel do if (iend-ista.ge.nth) private (j,k)
      DO i = ista,iend
!$omp parallel do if (iend-ista.lt.nth) private (k)
         DO j = 1,ny
            DO k = 1,nz
               vz(k,j,i) = 0.0_GP
            END DO
         END DO
      END DO

      R1 = 0.0_GP
      R2 = 0.0_GP
!$omp parallel do if (kend-ksta.ge.nth) private (j,i)
      DO k = ksta,kend
!$omp parallel do if (kend-ksta.lt.nth) private (i)
         DO j = 1,ny
            DO i = 1,nx

               R1(i,j,k) =                 SIN(2*pi*(real(i,kind=GP)-1)/   &
                          real(nx,kind=GP))*COS(2*pi*(real(j,kind=GP)-1)/  &
                          real(ny,kind=GP))*COS(2*pi*(real(k,kind=GP)-1)/  &
                          real(nz,kind=GP))
               R2(i,j,k) =                -COS(2*pi*(real(i,kind=GP)-1)/   &
                          real(nx,kind=GP))*SIN(2*pi*(real(j,kind=GP)-1)/  &
                          real(ny,kind=GP))*COS(2*pi*(real(k,kind=GP)-1)/  &
                          real(nz,kind=GP))

            END DO
         END DO
      END DO

      CALL fftp3d_real_to_complex(planrc,R1,vx,MPI_COMM_WORLD)
      CALL fftp3d_real_to_complex(planrc,R2,vy,MPI_COMM_WORLD)

      DO i = ista,iend
        DO j = 1,ny
          DO k = 1,nz
            IF ((kk2(k,j,i).lt.kdn**2).or.(kk2(k,j,i).gt.kup**2)) THEN
              vx(k,j,i) = 0.0
              vy(k,j,i) = 0.0
              vz(k,j,i) = 0.0
            ENDIF
          END DO
        END DO
     END DO

     CALL normalize(vx,vy,vz,u0,1,MPI_COMM_WORLD)
